#!/mod/bin/jimsh
#
# Render an importable Javascript containing the min and max date times, and the offset to the start of the TV day.
# Author: Martin Wink, 2013.
#

package require cgi
source /mod/webif/lib/setup

if { ![exists -proc sqlite3.open] } { package require sqlite3 }

#
# Open the database file just to get the min and max times for the calendar.
# Round them to the start & end of the day.
# Can't use ceil() & floor() as they aren't currently compiled into Jim.
#
if { ![file exists /mod/etc/tvdiary.db] } {
  set min_time 0
} else {
  set tvdiarydb [sqlite3.open /mod/etc/tvdiary.db]
  
  set _minstart [$tvdiarydb query "SELECT MIN(start) FROM activities"]
  if { [llength $_minstart] == 1 } {
    set min_time [expr int([lindex [lindex $_minstart 0] 1] / 86400) * 86400]
  } else {
    set min_time 0
  }
  $tvdiarydb close
}
set max_time [expr int(([clock seconds] + 86399) / 86400) * 86400]

# Get the configured start time of the TV day just once here, rather than on every time we get a day's data.
# Converted to seconds.
set day_start [expr (6 * 60 + 0) * 60]
if { ![catch {set settings_fd [open "/mod/etc/tvdiary.conf" r]}] } {
  foreach line [split [read $settings_fd] "\n"] {
    lassign $line settings_key settings_val
    switch $settings_key {
      "day_start" {
        set parts [split $settings_val ":"]
        set day_start [expr ([lindex $parts 0] * 60 + [lindex $parts 1]) * 60]
        break
      }
    }
  }
  $settings_fd close
}

httpheader "application/javascript"

puts "var min_time=$min_time;"
puts "var max_time=$max_time;"
puts "var day_start=$day_start;"
