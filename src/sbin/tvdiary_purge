#!/mod/bin/jimsh
#
# Purge the TV Diary database of old data.
#
# Author: Martin Wink, 2013.
#

package require sqlite3
source /mod/webif/html/tvdiary/tvdiary_db.jim

# --------------------------------------------------------------
# Declare globals
# --------------------------------------------------------------

set DB_FILEPATH "/mod/etc/tvdiary.db"

set current_time [clock seconds]
set seconds_per_day 86400

set log_date_fmt "%a %d %b %Y %H:%M"
set logging_time [clock format $current_time -format $log_date_fmt]

set usage_msg "Usage: $argv0 <num-days-to-retain> \[Y\]"

# --------------------------------------------------------------
# Declare procedures
# --------------------------------------------------------------

proc main {} {
  if { $::argc != 1 && $::argc != 2 } {
    puts $::usage_msg
    exit -1
  }
  set num_days [lindex $::argv 0]
  set yes_response 0
  if { $::argc == 2 && [string tolower [lindex $::argv 1]] == "y"} {
    set yes_response 1
  }

  if { $num_days < 1 || $num_days > 1000 } {
    puts $::usage_msg
    puts "  $num_days is not a sensible number of days."
    exit -1
  }

  if { ![file exists $::DB_FILEPATH] } {
    puts "Error: Unable to open the database file $::DB_FILEPATH"
    exit -1
  }

  set purge_time [expr $::current_time - ($num_days * $::seconds_per_day)]

  #
  # Get a lock for the database
  #
  if {![acquire_lock tvdiary_db]} {
    puts "Cannot acquire exclusive lock for tvdiary.tb. Terminating."
    exit -1
  }

  set tvdiarydb [sqlite3.open $::DB_FILEPATH]
  try {
    set _rec [$tvdiarydb query "SELECT COUNT(*) as count, MIN(start) as min, MAX(start) as max FROM activities WHERE start < %s" $purge_time]
    if { [llength $_rec] != 1 } {
      puts "Error: Couldn't query the number of events that will be purged."
      exit -1
    }
    set rec [lindex $_rec 0]

    set count $rec(count)
    set min_time $rec(min)
    set max_time $rec(max)

    if { $count == 0 } {
      puts "There are no events prior to [clock format $purge_time -format $::log_date_fmt]."
      exit
    }

    if { !$yes_response } {
      puts "Delete the $count events between [clock format $min_time -format $::log_date_fmt] and [clock format $max_time -format $::log_date_fmt]?"
      puts -nonewline "Continue? Y or N>"
      set line [gets stdin]
      puts $line

      if { [string tolower $line] == "y" } {
        set yes_response 1
      }
    } else {
      puts "Deleting $count events between [clock format $min_time -format $::log_date_fmt] and [clock format $max_time -format $::log_date_fmt]."
    }

    if { $yes_response } {
      $tvdiarydb query "BEGIN TRANSACTION"
      $tvdiarydb query "DELETE FROM activities WHERE start < %s" $purge_time
      set num_events [$tvdiarydb changes]

      set num_programs [purge_orphaned_programs $tvdiarydb]
      set num_channels [purge_orphaned_channels $tvdiarydb] 
      set num_titles [purge_orphaned_titles $tvdiarydb] 
      set num_synopses [purge_orphaned_synopses $tvdiarydb]

      $tvdiarydb query "COMMIT TRANSACTION"
      $tvdiarydb query "VACUUM"
      puts "Successfully deleted $num_events events, details of $num_programs programmes, $num_channels channel names, $num_titles titles, and $num_synopses synopses."

    } else {
      puts "OK. Leaving all of the data there."
    }

  } finally {
    $tvdiarydb close

    #
    # Closed the DB so release the lock.
    #
    release_lock tvdiary_db
  }
}

main
