#!/mod/bin/jimsh
#
# Aggregate statistics in the tvdiary database.
# To be "source" included in tvdiary_status. Not standalone.
# Author: Martin Wink, 2014.
#

# --------------------------------------------------------------
# Declare procedures
# --------------------------------------------------------------

#
# Main entry point. Returns 1 on success, 0 on failure.
#
proc do_aggregate { tvdiarydb current_time } {
  try {

    set settings_val [get_setting $tvdiarydb "day_start" "00:00"]
    set parts [split $settings_val ":"]
    set ::day_start_hour [lindex $parts 0]
    set ::day_start_min [lindex $parts 1]
    set ::day_start [expr (($::day_start_hour * 60) + $::day_start_min) * 60]

    set start_year_month [get_setting $tvdiarydb "start_aggregated_year_month" 201301]
    puts "Existing start_aggregated_year_month=$start_year_month"

    aggregate $tvdiarydb $start_year_month

    set date_parts [get_tv_date $current_time]
    set start_year_month [expr ([lindex $date_parts 0] * 100) + [lindex $date_parts 1]]
    puts "Setting start_aggregated_year_month=$start_year_month"
    set_setting $tvdiarydb "start_aggregated_year_month" $start_year_month

    set_setting $tvdiarydb "time_for_aggregation" [get_tv_day_start [expr $current_time + 86400]]
    puts "Time for the next aggregation: [clock format [get_tv_day_start [expr $current_time + 86400]]]"

    puts "Aggregation completed successfully."
    return 1
  } on error { msg } {
    puts "Aggregation failed. \[$msg\]"
    puts [info stacktrace]
    return 0
  }
}

#
# Calculate the aggregated summary statistics.
# Return the year_month of the start of the next month to aggregate.
#
proc aggregate { tvdiarydb start_year_month} {
  report_facts_count $tvdiarydb "Before purge, facts count"
  
  # Purge aggregated facts from previously incomplete months.
  $tvdiarydb query "DELETE FROM program_facts WHERE year_month >= %s;" $start_year_month
  set changes [$tvdiarydb changes]
  puts "Purged $changes program_facts rows."

  report_facts_count $tvdiarydb "After purge, facts count"

  # The activities use seconds from UTC epoc, so convert from local start of TV day year_month.
  set query_start_time [get_tv_month_time $start_year_month]
  
  # Get complete set of records, sorted so activities for a single program are adjacent in type and time order.
  set records [$tvdiarydb query "
    SELECT programs.program_id, programs.title_id, programs.channel_id, activities.type, activities.start, activities.end, programs.duration AS scheduled_duration
    FROM programs, activities
    WHERE programs.program_id = activities.program_id AND activities.start > %s
    ORDER BY programs.program_id, activities.start
  " $query_start_time]

  # Accumulate the details to be inserted into the facts table. Ajacent activities for the same program on the same day have their durations combined.
  # This ensures a single program recording split in two get counted as one. The type field in the accumlated record is useless.
  # The scheduled duration is counted only once per day.
  set ::accumulated_record {}

  foreach record $records {
    # Calculate the durations & count for this activity.
    if { $record(type) eq "R" } {
      set record(recorded_duration) [expr ($record(end) - $record(start)) / 60]
      set record(played_duration) 0
      set record(live_duration) 0
      set record(recorded_count) 1
      set record(played_count) 0
      set record(live_count) 0
    } elseif { $record(type) eq "P" } {
      set record(recorded_duration) 0
      set record(played_duration) [expr ($record(end) - $record(start)) / 60]
      set record(live_duration) 0
      set record(recorded_count) 0
      set record(played_count) 1
      set record(live_count) 0
    } else {
      set record(recorded_duration) 0
      set record(played_duration) 0
      set record(live_duration) [expr ($record(end) - $record(start)) / 60]
      set record(recorded_count) 0
      set record(played_count) 0
      set record(live_count) 1
    }

    # Get the date for the record.
    set date_parts [get_tv_date $record(start)]
    set record(year_month) [expr ([lindex $date_parts 0] * 100) + [lindex $date_parts 1]]

    if { [llength $::accumulated_record] } {
      if { [is_extension $record] } {
        # There is an accumulated record & this record extends it, so combine.
        combine_accumulated $record
      } else {
        # There is an accumulated record, but this record is for a different program or day, so save the accumulated record and restart accumulating with this record.
        save_accumulated $tvdiarydb
        set ::accumulated_record $record
      }
    } else {
      # There is no accumulated record, so start accumulating with this record.
      set ::accumulated_record $record
    }
  }

  # If there is an unsaved accumulated record, save it at the end.
  if { [llength $::accumulated_record] } {
    save_accumulated $tvdiarydb
    set ::accumulated_record {}
  }

  report_facts_count $tvdiarydb "After aggregation, facts count"
  #$tvdiarydb query "VACUUM;"
}

#
# Check whether the new record is an extension to the accumulated_record.
# That is, it is the same programme within the same month.
#
proc is_extension { record } {
  if { $::accumulated_record(program_id) == $record(program_id)
    && $::accumulated_record(year_month) == $record(year_month) } {
    return 1
  } else {
    return 0
  }
}

#
# Combine the details of a single record with the already accumulated record.
# Counts ORed together and durations added.
#
proc combine_accumulated { record } {
  set ::accumulated_record(recorded_duration) [expr $::accumulated_record(recorded_duration) + $record(recorded_duration)]
  set ::accumulated_record(played_duration) [expr $::accumulated_record(played_duration) + $record(played_duration)]
  set ::accumulated_record(live_duration) [expr $::accumulated_record(live_duration) + $record(live_duration)]
  set ::accumulated_record(recorded_count) [expr $::accumulated_record(recorded_count) || $record(recorded_count)]
  set ::accumulated_record(played_count) [expr $::accumulated_record(played_count) || $record(played_count)]
  set ::accumulated_record(live_count) [expr $::accumulated_record(live_count) || $record(live_count)]
}

#
# Add the accumulated record to the database.
#
proc save_accumulated { tvdiarydb } {
  if { $::accumulated_record(played_duration) + $::accumulated_record(live_duration) < ($::accumulated_record(scheduled_duration) / 2) } {
    set barely_watched_count 1
  } else {
    set barely_watched_count 0
  }

  # Fetch any existing record having the same dimensions - ie same program in same time period - and combine with the accumulated record.
  set _facts [$tvdiarydb query "SELECT fact_id, recorded_count, played_count, live_count, scheduled_duration, recorded_duration, played_duration, live_duration, barely_watched_count FROM program_facts WHERE year_month = $::accumulated_record(year_month) AND title_id = $::accumulated_record(title_id) AND channel_id = $::accumulated_record(channel_id)"]
  if { [llength $_facts] eq 1 } {
    set facts [lindex $_facts 0]
    set recorded_count [expr $::accumulated_record(recorded_count) + $facts(recorded_count)]
    set played_count [expr $::accumulated_record(played_count) + $facts(played_count)]
    set live_count [expr $::accumulated_record(live_count) + $facts(live_count)]
    set scheduled_duration [expr $::accumulated_record(scheduled_duration) + $facts(scheduled_duration)]
    set recorded_duration [expr $::accumulated_record(recorded_duration) + $facts(recorded_duration)]
    set played_duration [expr $::accumulated_record(played_duration) + $facts(played_duration)]
    set live_duration [expr $::accumulated_record(live_duration) + $facts(live_duration)]
    set barely_watched_count [expr $barely_watched_count + $facts(barely_watched_count)]
    $tvdiarydb query "
      UPDATE program_facts SET recorded_count = %s, played_count = %s, live_count = %s, scheduled_duration = %s, recorded_duration = %s, played_duration = %s, live_duration = %s, barely_watched_count = %s WHERE fact_id = %s
      " $recorded_count $played_count $live_count $scheduled_duration $recorded_duration $played_duration $live_duration $barely_watched_count $facts(fact_id)
  } else {
    $tvdiarydb query "
      INSERT INTO program_facts (year_month, title_id, channel_id, recorded_count, played_count, live_count, scheduled_duration, recorded_duration, played_duration, live_duration, barely_watched_count)
      VALUES (%s, %s, %s, %s, %s, %s, %s, %s, %s, %s, %s)
      " $::accumulated_record(year_month) $::accumulated_record(title_id) $::accumulated_record(channel_id) $::accumulated_record(recorded_count) $::accumulated_record(played_count) $::accumulated_record(live_count) $::accumulated_record(scheduled_duration) $::accumulated_record(recorded_duration) $::accumulated_record(played_duration) $::accumulated_record(live_duration) $barely_watched_count
  }
}

#
# Convert time of day to time of the start of that TV day.
# NB The TZ offset in Tcl is opposite sign to JavaScript.
#
proc get_tv_day_start { t } {
  set z [clock format $t -format "%z"]
  set tz_offset [expr int($z / 100) * 3600 + int($z % 100) * 60]
  set day_t [expr int( ($t + $tz_offset - $::day_start) / 86400 ) * 86400 - $tz_offset + $::day_start]
  return $day_t
}

#
# Convert the given time to the TV date it belongs to. The input time is a UTC epoch value.
# Since the start of TV day is in the local time, convert from UTC to local first, check which side
# of the TV day start it is. If it's before, adjust the time back and re-calculate the local time.
# Returns a list containing the year, month and day numbers.
#
proc get_tv_date { t } {
  set locparts [split [clock format $t -format "%Y,%m,%d,%H,%M"] ","]
  set day_time [expr ([lindex $locparts 3] * 60 + [lindex $locparts 4]) * 60]
  if { $day_time < $::day_start } {
    set locparts [split [clock format [expr $t - $::day_start] -format "%Y,%m,%d,%H,%M"] ","]
  }
  return [list [lindex $locparts 0] [lindex $locparts 1] [lindex $locparts 2] ]
}

#
# Convert year_month to a UTC time for querying activities.
#
proc get_tv_month_time { year_month } {
  set year [expr int( $year_month / 100)]
  set month [expr int( $year_month % 100)]
  set date_str "$year $month 1 $::day_start_hour $::day_start_min 0"
  set result [clock scan $date_str -format "%Y %m %d %H %M %S"]
  return $result
}

proc report_facts_count { tvdiarydb label } {
  set res [$tvdiarydb query "SELECT COUNT(*) FROM program_facts"]
  set count [lindex [lindex $res end] end]
  puts "$label: $count"
}




# Experiment - this is slower 
proc get_tv_date2 { t } {
  set day_t [get_tv_day_start $t]
  set locparts [split [clock format $day_t -format "%Y,%m,%d,%H,%M"] ","]
  return [list [lindex $locparts 0] [lindex $locparts 1] [lindex $locparts 2] ]
}
proc test_date_converter {} {
  set parts [split "04:00" ":"]
  set ::day_start_hour [lindex $parts 0]
  set ::day_start_min [lindex $parts 1]
  set ::day_start [expr (($::day_start_hour * 60) + $::day_start_min) * 60]

  for {set y 2013} {$y < 2015} {incr y} {
    for {set m 1} {$m <= 12} {incr m} {
      set ym [expr $y * 100 + $m]
      puts "Test month $y $m, $ym"
      set r [get_tv_month_time $ym]
      puts "Start time for month $ym = [clock format $r]"
    }
  }
  return

  set t_start [clock scan "2014,3,28,0,5" -format "%Y,%m,%d,%H,%M"]
  set t_end [clock scan "2014,4,1,0,0" -format "%Y,%m,%d,%H,%M"]
  for {set t $t_start} {$t < $t_end} {incr t 3600} {
    puts "$t=[clock format $t], get_tv_day_start=[get_tv_day_start $t], get_tv_date2=[get_tv_date2 $t], get_tv_date=[get_tv_date $t]"
  }
  puts [time {get_tv_date2 $t} 1000]
  puts [time {get_tv_date $t} 1000]
}
#test_date_converter
