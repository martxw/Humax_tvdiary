#!/mod/bin/jimsh
#
# Render an importable Javascript containing the min and max date times, and the offset to the start of the TV day.
# Author: Martin Wink, 2013.
#

package require cgi
source /mod/webif/lib/setup
source /mod/webif/html/tvdiary/tvdiary_db.jim

if { ![exists -proc sqlite3.open] } { package require sqlite3 }

set DB_FILEPATH "/mod/etc/tvdiary.db"
set CONF_FILEPATH "/mod/etc/tvdiary.conf"

#
# Open the database file just to get the min and max times for the calendar.
# Round them to the start & end of the day.
# Can't use ceil() & floor() as they aren't currently compiled into Jim.
#
if { ![file exists $DB_FILEPATH] } {
  set min_time 0
} else {
  #
  # Get a lock for the database
  #
  if {![acquire_lock tvdiary_db]} {
    puts "Cannot acquire exclusive lock for tvdiary.tb."
    exit -1
  }

  set tvdiarydb [sqlite3.open $DB_FILEPATH]
  
  set _minstart [$tvdiarydb query "SELECT MIN(start) FROM activities"]
  if { [llength $_minstart] == 1 } {
    set min_time [expr int([lindex [lindex $_minstart 0] 1] / 86400) * 86400]
  } else {
    set min_time 0
  }
  $tvdiarydb close

  #
  # Closed the DB so release the lock.
  #
  release_lock tvdiary_db
}
set max_time [expr (int([clock seconds] / 86400) + 7) * 86400]

# Get the configured start time of the TV day just once here, rather than on every time we get a day's data.
# Converted to seconds. Default here to 00:00 (0 * 60 + 0).
set day_start [expr (0 * 60 + 0) * 60]
try {
  set settings_fd [open $CONF_FILEPATH r]
  foreach line [split [read $settings_fd] "\n"] {
    lassign $line settings_key settings_val
    switch $settings_key {
      "day_start" {
        set parts [split $settings_val ":"]
        set day_start [expr ([lindex $parts 0] * 60 + [lindex $parts 1]) * 60]
        break
      }
    }
  }
  $settings_fd close
} on error { msg } {
  # Use the default if no file.
}

httpheader "application/javascript"

puts "var min_time=$min_time;"
puts "var max_time=$max_time;"
puts "var day_start=$day_start;"
