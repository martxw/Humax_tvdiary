#!/mod/bin/jimsh
#
# Render the details of all program activities sharing either the title and synopsis,
# or just the title, of the one identified by the program or title ID.
# If the program ID is supplied then the title and synopsis text is matched.
# If the title ID is supplied then just the title is matched.
# All of their record and playback activities are listed.
# After successfully rendering the data, the value "status" is set to "OK".
# If there is an error, "status" is set to the "<error message>".
# Clients should therefore check "status" first. If there is a serious server error part way
# through rendering, the JSON may be malformed and not parsable.
#
# CGI usage: /tvdiary/history_json.jim?prog_id=<program_id>|crid=<event_crid>|title_id=<title_id>|channel_id=<channel_id>|title=<title>|synopsis=<synopsis>|channel=<channel>
#   program_id = the program ID.
#
#   event_crid = the program's CRID.
#
#   title_id = the title ID.
#   channel_id = the channel ID.
#
#   title = the title text.
#   synopsis = the synopsis text.
#   channel = the channel text.
#   title_op = the title operator [E|C|S|F].
#   synopsis_op = the synopsis operator [E|C|S|F].
#   channel_op = the channel operator [E|C|S|F].
#
# Author: Martin Wink, 2014.
#

package require cgi
source /mod/webif/lib/setup
require mwildcard
source /mod/webif/html/tvdiary/tvdiary_utils.jim
source /mod/webif/html/tvdiary/tvdiary_stub.jim

# --------------------------------------------------------------
# Declare globals
# --------------------------------------------------------------
set current_time [clock seconds]
set time_fmt "%H:%M"
set date_time_fmt "%a %d %b %Y $time_fmt"

set channel_icons_path "/mod/webif/html/img/channels"
set channel_icons_url "/img/channels"

set program_id_specified 0
set crid_specified 0
set title_id_specified 0
set channel_id_specified 0

# --------------------------------------------------------------
# CGI parameters
# --------------------------------------------------------------
set runmode cli
if { [dict exists $env "REQUEST_METHOD"] } { set runmode cgi }

case $runmode {
  cgi {
    cgi_input
    set program_id_specified [cgi_exists program_id]
    set crid_specified [cgi_exists crid]
    set title_id_specified [cgi_exists title_id]
    set channel_id_specified [cgi_exists channel_id]

    set program_id [cgi_get program_id]

    set crid [cgi_get crid]

    set title_id [cgi_get title_id]
    set channel_id [cgi_get channel_id]

    set title [cgi_get title]
    set synopsis [cgi_get synopsis]
    set channel [cgi_get channel]

    set title_op [cgi_get title_op]
    set synopsis_op [cgi_get synopsis_op]
    set channel_op [cgi_get channel_op]
    httpheader "application/json"
  }
  cli {
    set program_id ""
    set crid ""
    set title_id ""
    set channel_id ""
    set title ""
    set synopsis ""
    set channel ""
    set title_op ""
    set synopsis_op ""
    set channel_op ""
    if { $argc > 1 } {
      set type [lindex $argv 0]

      if { $type eq "P" } {
        set program_id [lindex $argv 1]
        set channel_icons_path [lindex $argv 2]
        set channel_icons_url [lindex $argv 3]
        set program_id_specified 1
      } elseif { $type eq "C" } {
        set crid [lindex $argv 1]
        set channel_icons_path [lindex $argv 2]
        set channel_icons_url [lindex $argv 3]
        set crid_specified 1
      } elseif { $type eq "TC" } {
        set title_id [lindex $argv 1]
        set channel_id [lindex $argv 2]
        set channel_icons_path [lindex $argv 3]
        set channel_icons_url [lindex $argv 4]
        set title_id_specified 1
        set channel_id_specified 1
      } elseif { $type eq "S" } {
        set title_op [lindex $argv 1]
        set title [lindex $argv 2]
        set synopsis_op [lindex $argv 3]
        set synopsis [lindex $argv 4]
        set channel_op [lindex $argv 5]
        set channel [lindex $argv 6]
        set channel_icons_path [lindex $argv 7]
        set channel_icons_url [lindex $argv 8]
      }
    }
  }
}

# In case of error - break glass!
#puts "/* "
#foreach key [lsort [dict keys $env]] {puts "env($key)=$env($key)"}
#puts "auto_path=$auto_path"
#puts "*/"

# --------------------------------------------------------------
# Declare procedures
# --------------------------------------------------------------

#
# Convert from op code to operator text.
#
proc op_code_to_operator { op } {
  if { $op eq "E" } {
    return "="
  } else {
    return "LIKE"
  }
}

#
# Convert from op code to parameter text.
#
proc op_code_to_parameter { op val } {
  if { $op eq "C" } {
    return "%[mwildcard $val]%"
  } elseif { $op eq "S" } {
    return "[mwildcard $val]%"
  } elseif { $op eq "F" } {
    return "%[mwildcard $val]"
  } else {
    return "[mwildcard $val]"
  }
}

#
# Main
#
proc main {} {
  # begin_debug
  #puts "/* Parameters:
  #  program_id=$::program_id
  #  crid=$::crid
  #  title_id=$::title_id
  #  channel_id=$::channel_id
  #  title=$::title
  #  synopsis=$::synopsis
  #  channel=$::channel */"
  #  title_op=$::title_op
  #  synopsis_op=$::synopsis_op
  #  channel_op=$::channel_op */"
  # end_debug

  #
  # Check we have a database, and open it, else HTTP error.
  #
  if { ![file exists $::DB_FILEPATH] } {
    # Ajax detects the error if we just exit.
    throw error "The tvdiary.db database is missing."
  }

  #
  # Get a lock for the database
  #
  if {![acquire_lock tvdiary_db]} {
    throw error "Timed out acquiring exclusive access to the database. Try again later."
  }

  set tvdiarydb [sqlite3.open $::DB_FILEPATH]

  # Separate queries to populate the records depending on parameters.
  if { $::program_id_specified } {
    # If the program ID is supplied, find the title_id and synopsis_id to search on, and their text to return in the JSON.
    set records [$tvdiarydb query "
      SELECT programs.title_id, programs.synopsis_id, titles.title, synopses.synopsis
      FROM programs, titles, synopses
      WHERE program_id =  %s AND programs.title_id = titles.title_id AND programs.synopsis_id = synopses.synopsis_id
      LIMIT 1" $::program_id]
    set record [lindex $records 0]
    set ::title_id $record(title_id)
    set ::synopsis_id $record(synopsis_id)
    set ::title $record(title)
    set ::synopsis $record(synopsis)

    # The records exclude the title and synopsis because they're the search criteria.
    set records [$tvdiarydb query "
      SELECT activities.type, activities.start, activities.end, channels.channel_name, activities.program_id, programs.crid as event_crid,
        (SELECT COUNT(*) FROM programs p2 WHERE p2.title_id = programs.title_id AND p2.synopsis_id = programs.synopsis_id) AS repeat_count,
        (SELECT COUNT(*) FROM programs p3 WHERE p3.crid = programs.crid AND p3.program_id != programs.program_id AND LENGTH(programs.crid) > 0) as repeat_crid_count,
        (SELECT COUNT(*) FROM inventory WHERE inventory.channel_id = programs.channel_id AND inventory.scheduled_start = programs.start) AS available
      FROM activities, programs, channels
      WHERE programs.title_id = %s
        AND programs.synopsis_id = %s
        AND activities.end != 0
        AND activities.program_id = programs.program_id
        AND programs.channel_id = channels.channel_id
      ORDER BY activities.start" $::title_id $::synopsis_id]
  } elseif { $::crid_specified } {
    # If the CRID is supplied, do the search on program entries with that crid.
    # The records include the title and synopsis because they're not guaranteed to be the same.
    set records [$tvdiarydb query "
      SELECT activities.type, activities.start, activities.end, channels.channel_name, titles.title, synopses.synopsis, activities.program_id, programs.crid as event_crid,
        (SELECT COUNT(*) FROM programs p2 WHERE p2.title_id = programs.title_id AND p2.synopsis_id = programs.synopsis_id) AS repeat_count,
        (SELECT COUNT(*) FROM programs p3 WHERE p3.crid = programs.crid AND p3.program_id != programs.program_id AND LENGTH(programs.crid) > 0) as repeat_crid_count,
        (SELECT COUNT(*) FROM inventory WHERE inventory.channel_id = programs.channel_id AND inventory.scheduled_start = programs.start) AS available
      FROM activities, programs, channels, titles, synopses
      WHERE programs.crid = '%s'
        AND activities.end != 0
        AND activities.program_id = programs.program_id
        AND programs.channel_id = channels.channel_id
        AND programs.title_id = titles.title_id
        AND programs.synopsis_id = synopses.synopsis_id
      ORDER BY activities.start" $::crid]

    # Dummy the title text to be the first found. Not guaranteed identical for all, but it looks better than no title at all.
    set record [lindex $records 0]
    set ::title $record(title)
    set ::title_op "C"
  } elseif { $::title_id_specified } {
    # If the title_id supplied, then exact match, with the channel_id if it's supplied.
    set records [$tvdiarydb query "SELECT titles.title FROM titles WHERE titles.title_id = %s LIMIT 1" $::title_id]
    set record [lindex $records 0]
    set ::title $record(title)

    if { $::channel_id_specified } {
      set records [$tvdiarydb query "SELECT channels.channel_name FROM channels WHERE channels.channel_id = %s LIMIT 1" $::channel_id]
      set record [lindex $records 0]
      set ::channel_name $record(channel_name)

      set records [$tvdiarydb query "
        SELECT activities.type, activities.start, activities.end, channels.channel_name, titles.title, synopses.synopsis, activities.program_id, programs.crid as event_crid,
          (SELECT COUNT(*) FROM programs p2 WHERE p2.title_id = programs.title_id AND p2.synopsis_id = programs.synopsis_id) AS repeat_count,
          (SELECT COUNT(*) FROM programs p3 WHERE p3.crid = programs.crid AND p3.program_id != programs.program_id AND LENGTH(programs.crid) > 0) as repeat_crid_count,
          (SELECT COUNT(*) FROM inventory WHERE inventory.channel_id = programs.channel_id AND inventory.scheduled_start = programs.start) AS available
        FROM activities, programs, channels, titles, synopses
        WHERE programs.title_id = %s AND programs.channel_id = %s
          AND activities.end != 0
          AND activities.program_id = programs.program_id
          AND programs.channel_id = channels.channel_id
          AND programs.title_id = titles.title_id
          AND programs.synopsis_id = synopses.synopsis_id
        ORDER BY activities.start" $::title_id $::channel_id]
    } else {
      set records [$tvdiarydb query "
        SELECT activities.type, activities.start, activities.end, channels.channel_name, titles.title, synopses.synopsis, activities.program_id, programs.crid as event_crid,
          (SELECT COUNT(*) FROM programs p2 WHERE p2.title_id = programs.title_id AND p2.synopsis_id = programs.synopsis_id) AS repeat_count,
          (SELECT COUNT(*) FROM programs p3 WHERE p3.crid = programs.crid AND p3.program_id != programs.program_id AND LENGTH(programs.crid) > 0) as repeat_crid_count,
          (SELECT COUNT(*) FROM inventory WHERE inventory.channel_id = programs.channel_id AND inventory.scheduled_start = programs.start) AS available
        FROM activities, programs, channels, titles, synopses
        WHERE programs.title_id = %s
          AND activities.end != 0
          AND activities.program_id = programs.program_id
          AND programs.channel_id = channels.channel_id
          AND programs.title_id = titles.title_id
          AND programs.synopsis_id = synopses.synopsis_id
        ORDER BY activities.start" $::title_id]
    }
  } else {
    # Check parameters were supplied.
    if { $::title_op == {} || $::synopsis_op == {} || $::channel_op == {} } {
      throw error "All comparison operators must be supplied."
    }
    if { $::title == {} && $::synopsis == {} && $::channel == {} } {
      throw error "At least one search value must be specified."
    }

    # If not program or title ID, then the raw text parameters.
    set title_operator [op_code_to_operator $::title_op]
    set synopsis_operator [op_code_to_operator $::synopsis_op]
    set channel_operator [op_code_to_operator $::channel_op]
    set records [$tvdiarydb query "
      SELECT activities.type, activities.start, activities.end, channels.channel_name, titles.title, synopses.synopsis, activities.program_id, programs.crid as event_crid,
        (SELECT COUNT(*) FROM programs p2 WHERE p2.title_id = programs.title_id AND p2.synopsis_id = programs.synopsis_id) AS repeat_count,
        (SELECT COUNT(*) FROM programs p3 WHERE p3.crid = programs.crid AND p3.program_id != programs.program_id AND LENGTH(programs.crid) > 0) as repeat_crid_count,
        (SELECT COUNT(*) FROM inventory WHERE inventory.channel_id = programs.channel_id AND inventory.scheduled_start = programs.start) AS available
      FROM activities, programs, channels, titles, synopses
      WHERE ('%s' = '' OR titles.title $title_operator '%s')
        AND ('%s' = '' OR channels.channel_name $channel_operator '%s')
        AND ('%s' = '' OR synopses.synopsis $synopsis_operator '%s')
        AND activities.end != 0
        AND activities.program_id = programs.program_id
        AND programs.channel_id = channels.channel_id
        AND programs.title_id = titles.title_id
        AND programs.synopsis_id = synopses.synopsis_id
      ORDER BY activities.start
      LIMIT 1001" $::title [op_code_to_parameter $::title_op $::title] $::channel [op_code_to_parameter $::channel_op $::channel] $::synopsis [op_code_to_parameter $::synopsis_op $::synopsis]]
  }

  # Finished with the DB.
  $tvdiarydb close

  #
  # Closed the DB so release the lock.
  #
  release_lock tvdiary_db

  puts "{"
  if { $::program_id_specified } {
    puts " \"program_id\": $::program_id,"
    puts " \"title_op\": \"E\","
    puts " \"synopsis_op\": \"E\","
    puts " \"title\": \"[escape_json_string $::title]\","
    puts " \"synopsis\": \"[escape_json_string $::synopsis]\","
  } elseif { $::crid_specified } {
    puts " \"crid\": \"$::crid\","
    puts " \"title_op\": \"[escape_json_string $::title_op]\","
    puts " \"title\": \"[escape_json_string $::title]\","
  } elseif { $::title_id_specified } {
    puts " \"title_id\": $::title_id,"
    puts " \"title_op\": \"E\","
    puts " \"title\": \"[escape_json_string $::title]\","
    if { $::channel_id_specified } {
      puts " \"channel_id\": $::channel_id,"
      puts " \"channel_name_op\": \"E\","
      puts " \"channel_name\": \"[escape_json_string $::channel_name]\","
    }
  } else {
    puts " \"title_op\": \"[escape_json_string $::title_op]\","
    puts " \"title\": \"[escape_json_string $::title]\","
    puts " \"synopsis_op\": \"[escape_json_string $::synopsis_op]\","
    puts " \"synopsis\": \"[escape_json_string $::synopsis]\","
    puts " \"channel_name_op\": \"[escape_json_string $::channel_op]\","
    puts " \"channel_name\": \"[escape_json_string $::channel]\","
  }
  puts -nonewline " \"events\": \["

  if { [llength $records] > 0 } {
    set comma 0
    foreach record $records {
      if { $comma } {
        puts ","
      } else {
        puts ""
        set comma 1
      }

      # Default in case of old W records.
      set typeclass "play"
      case $record(type) {
        R {
          set typeclass "record"
        }
        P {
          set typeclass "play"
        }
        L {
          set typeclass "live"
        }
      }

      puts "{"
      puts " \"type\": \"$typeclass\","
      puts " \"program_id\": $record(program_id),"
      puts " \"start\": $record(start),"
      puts " \"end\": $record(end),"
      puts " \"repeat_count\": $record(repeat_count),"
      puts " \"repeat_crid_count\": $record(repeat_crid_count),"
      puts " \"event_crid\": \"$record(event_crid)\","
      puts " \"available\": $record(available),"
      
      if { $::program_id_specified } {
        puts " \"title\": \"[escape_json_string $::title]\","
        puts " \"synopsis\": \"[escape_json_string $::synopsis]\","
      } else {
        puts " \"title\": \"[escape_json_string $record(title)]\","
        puts " \"synopsis\": \"[escape_json_string $record(synopsis)]\","
      }
      puts " \"channel_name\": \"[escape_json_string $record(channel_name)]\","

      set channel_icon_path "$::channel_icons_url/blank.png"
      if { $record(channel_name) != "" } {
        if { [file exists "$::channel_icons_path/$record(channel_name).png"] } {
          set channel_icon_path "$::channel_icons_url/[cgi_quote_url $record(channel_name)].png"
        } else {
          set channel_icon_path "$::channel_icons_url/blank.png"
        }
      }
      puts " \"channel_icon_path\": \"$channel_icon_path\""

      puts -nonewline "}"
    }
  }
  puts ""
  puts " \],"

  puts " \"status\": \"OK\""
  puts "}"
}

try {
  main
} on error { msg } {
  puts "{ \"status\": \"[escape_json_string $msg]\" }"
}
